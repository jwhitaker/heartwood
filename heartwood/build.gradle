plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion '8.25'
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }

    return stdout.toString().trim().substring(1)
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/jwhitaker/heartwood")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_API_KEY")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            groupId = 'ca.thewhitakers.heartwood'
            artifactId = 'heartwood'
            version = getVersionName()
            from(components.java)
            artifact sourcesJar
            artifact javadocJar
        }
    }

}

task whitaker {
    doLast {
        println(getVersionName())
    }
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.5.2")
    testImplementation("org.hamcrest:hamcrest-library:2.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.5.2")    
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
